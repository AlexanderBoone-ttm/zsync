dnl Process this file with autoconf to produce a configure script.
AC_INIT([zsync],[0.6])
AC_CONFIG_SRCDIR([librcksum/rsum.c])
AC_CONFIG_AUX_DIR(autotools)
AM_INIT_AUTOMAKE([dist-bzip2 no-dist-gzip foreign check-news -Woverride -Wobsolete -Wportability -Wsyntax -Wunsupported])
AM_MAINTAINER_MODE
AC_CONFIG_HEADERS([config.h])

dnl --- Check for programs
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_RANLIB

CFLAGS="$CFLAGS -g -Wall -I\$(top_srcdir)"
AC_C_COMPILE_FLAGS(-Wwrite-strings -Winline -Wextra -Winline -Wmissing-noreturn -Wredundant-decls -Wnested-externs -Wundef -Wbad-function-cast -Wcast-align -Wvolatile-register-var -ffast-math)
AC_ARG_ENABLE(profile,[  --enable-profile         turns on profiling],
CFLAGS_OPT="-pg"
)

dnl --- Header files, typedefs, structures, libraries
AC_C_CONST
AC_HEADER_STDC
# string.h, memory.h, stdlib.h both included in the default header checks
# but we do need to give at least one .h to test, or Solaris sh errors
AC_CHECK_HEADERS([string.h])
AC_TYPE_SIZE_T
AC_CHECK_FUNCS(memcpy pwrite)
AC_TYPE_SOCKLEN_T
AC_TYPE_IN_PORT_T
AC_DECL_H_ERRNO

dnl Solaris needs -lsocket - and we need this for the getaddrinfo test
AC_CHECK_LIB(socket,socket)

AC_REPLACE_FUNCS(getaddrinfo)

dnl - Large file support if available
AC_SYS_LARGEFILE
AC_FUNC_FSEEKO
AC_CHECK_SIZEOF(size_t)
AC_CHECK_SIZEOF(off_t)

AM_WITH_DMALLOC
AH_BOTTOM([
#ifdef WITH_DMALLOC
# include <dmalloc.h>
#endif
])

dnl --- output
AC_SUBST(ac_aux_dir)
AC_CONFIG_FILES([Makefile librcksum/Makefile zlib/Makefile libzsync/Makefile doc/Makefile])
AC_OUTPUT

